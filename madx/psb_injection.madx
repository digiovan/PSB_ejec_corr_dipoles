
/******************************************************************************************
 *
 * PS BOOSTER. Directory: /afs/cern.ch/eng/ps/cps/Psb
 * It consists of 16 identical periods, apart from the equipment
 * in the straight sections.
 * The BOOSTER has a radius of 25m and thus a circonference of 157.08m
 *
 * The injected beam from the linac have Ekin=50 MeV proton beam with p~300MeV/c.
 * (311 MeV/c according to the report: http://accelconf.web.cern.ch/accelconf/p05/PAPERS/TPAT054.PDF)
 * corresponding to E=0.988471 GeV and E0=0.938272 GeV. (The rest mass of a proton is 0.938272 GeV)
 * The kinetic energy of the extracted beam is Ekin=1.4 GeV and p=2.141766 GeV/c [p=(SQRT(Ekin^2+2*E0*Ekin)/c]
 * The beam is extracted to the PS. In the PS, it is injected in SS42.
 *
 * Revolution time at extraction:  T,revolution,ext = 572.79 ns (nano  seconds)
 * Revolution time at injection:   T,revolution,inj =   1.6  us (micro seconds)
 * The BOOSTER today has 2 bunches in each ring, i.e. 8 bunches in the PS.
 * Injection time: C=275 ms, measure time C=290 ms
 *
 * Working points:
 * Injection - high intensity          : QX=4.28,  QY=4.55
 * Injection - low  intensity e.g. LHC : QX=4.28,  QY=4.45
 * Extraction                          : QX=4.172, QY=4.23   = wp1
 *
 *
 *
 *
 * The 2 MHz cavities are for the H0 mode, they are run around 8kV.
 * They are run at the revolution frequency.
 * The beam is below transition.
 *
 * The 4 MHz cavities are used to flatten the bunches.
 * They reduce the peak current compared to the average currents.
 * The 4 MHz cavities are run between 6-8 kV.
 * They are run at twice the revolution frequency.
 *
 *
 * All elements in the straight sections added.  21 Dec 2006 O.Berrig
 * New working point: QX = 4.172 and QY = 4.23.  21 Dec 2006 O.Berrig
 *
 *
 ******************************************************************************************
 *
 * This file is for protons at 0.311 GeV/c
 *
 *
 * Execute with:  >madx_dev < psb_injection.madx
 *
 ******************************************************************************************/


 title, 'BOOSTER lattice';

 option, echo;
 option, RBARC=FALSE;



/******************************************************************************************
 * BOOSTER
 ******************************************************************************************/
 call, file = '../psb.ele';
 call, file = '../psb.seq';
 call, file = '../psb.dbx';
 call, file = '../strength/psb_injection.str';



!save, sequence=psb1, file=psb1.save;



/******************************************************************************************
 * beam, use
 ******************************************************************************************/

beam, particle=PROTON, pc=0.311, exn=15E-6*3.0,eyn=8E-6*3.0, sige=1.35E-3*3.0, sigt=230E-9 ;  ! 3 sigma ISOLDE type beam.
use, sequence=psb1;

set,  format="20.10f";

set,  format="10.05f";


/******************************************************************************************
 * Match for new working point
 ******************************************************************************************/

MATCH,sequence=psb1;
 vary, NAME=kKF, step = 0.0001;
 vary, NAME=kKD, step = 0.0001;
 constraint, range=#E, MUX=4.28, MUY=4.45;
 lmdif, calls = 10000, tolerance = 1.0E-21;
ENDMATCH;

! Print results on file: match_injection.prt
assign, echo="../output/match_injection.prt";
print, text="match_injection";
value,  kKF;
value,  kKD;
assign, echo=terminal;




/******************************************************************************************
 * TWISS
 ******************************************************************************************/

PSHIFT=0;

/************************************************************
 * MAD uses pt as the 5th variable NOT delta-p.
 * The result is that all derivatives are with repect to pt.
 * This is the reason for the non-standard dispersion values
 * for non-relativistic machines, like the PSB
 * Ax and Ay are the apertures in number of sigmas
 ************************************************************/
 beta=sqrt(1-1/beam->gamma^2);
 disp:=beta*table(twiss,dx); ! Delta_x=disp*Delta_p/p;
!hx:=table(twiss,aper_1);
 hx := (table(twiss,aper_1)+table(twiss,aper_3)-abs(table(twiss,aper_1)-table(twiss,aper_3)))/2; ! MIN( aper_1, aper_3)
 hy := (table(twiss,aper_2)+table(twiss,aper_4)-abs(table(twiss,aper_2)-table(twiss,aper_4)))/2; ! MIN( aper_2, aper_4)
 sx := SQRT(table(twiss,betx)*beam->ex);
 sy := SQRT(table(twiss,bety)*beam->ey);
 option, -warn;
 Ax := SQRT((hx^2-(3*disp*beam->sige)^2)/(table(twiss,betx)*beam->ex));
 Ay := SQRT(hy^2/(table(twiss,bety)*beam->ey)); ! There is no dispersion in vertical plane
 option, warn;

select, flag=twiss, clear;
select, flag=twiss, column=name, s,x,alfx,alfy,betx,bety,disp,apertype,aper_1,aper_2,aper_3,aper_4,hx,hy,sx,sy,Ax,Ay;

twiss , centre
      , DELTAP = PSHIFT
      , table=TWISS
      , file='../output/output_injection.outx';

value, beam->ex, beam->ey,beam->sige, beam->sigt;
value,table(TWISS,PSB1$START,betx);



/******************************************************************************************
 * PLOT
 ******************************************************************************************/

/* Use >gv psb_injection to plot */

PLOT, table=TWISS, VAXIS=BETX,BETY,HAXIS=S,
      SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, FILE='../output/psb_injection'
PLOT, table=TWISS, VAXIS=DISP,HAXIS=S,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E;
PLOT, table=TWISS, VAXIS=ALFX,ALFY,HAXIS=S,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E;


STOP;

