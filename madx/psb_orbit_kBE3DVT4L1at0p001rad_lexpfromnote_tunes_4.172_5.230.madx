title, 'BOOSTER lattice @ 1.4 GeV';

option, echo;
option, RBARC=FALSE;



/******************************************************************************************
 * BOOSTER
 ******************************************************************************************/
call, file = '../psb.ele';
call, file = '../psb_expfromnote.seq';
call, file = '../psb.dbx';
call, file = '../strength/psb_extraction.str';


/******************************************************************************************
 * beam, use
 ******************************************************************************************/
beam, particle=PROTON, pc=2.14;  ! change depending on c-time of orbit measurement; pc=momentum
use, sequence=psb3;

set,  format="20.10f";

! useful for debugging
dumpsequ, sequence=psb3, level=2; 


/******************************************************************************************
 * IMPORTANT REDEFINITION OF THE SEPTUM TO GET INFO AT THE CENTER AND THE BEGINNING
 ******************************************************************************************/
! first redefine the SEPTUM to have length = 0 so to add its starting point
BE3_SMH15L1_POS = BE3.SMH15L1->at;
value, BE3.SMH15L1->at;
value, BE3_SMH15L1_POS;
 
BE3.SMH15L1 : HKICKER, L := 0, AT = BE3_SMH15L1_POS;


seqedit,sequence=P15TOPMID;
! install, element = BE3.SMH15L1_START, class=MARKER, at=BE3_SMH15L1_POS-0.63; ! length of the septum is 1.26 m
! now accounting the fact that the blade is smaller than the tank size
! and not even centered. Based on drawings from M. Houricane, PS.CA.9841.1
! assuming the madx sequence contains the center position how much should I move down to reach the starting of the septum blade?
! 
! |-A--|--------B------|--C--|, A=distance tank-beginning blade, B= blade length, C=distance end blade-tank
!       <- X ->|
!              0 <- this is the center
! 
! A+X = B-X+C  <=> X = (B+C-A)/2
!
! A=0.12103, B=1.00024, C=138.21   => X=0.508706
!
install, element = BE3.SMH15L1_START, class=MARKER, at=BE3_SMH15L1_POS-0.508706; ! length of the septum is 1.26 m
flatten;
endedit;

! needed to be re-used b/c otherwise MADX will keep using the first one expanded
! by the "use" command 
use, sequence=psb3;



/******************************************************************************************
 * ACTIVATE KICKER BE3.DVT4L1, CORRECTION OF 0.001 rad
 ******************************************************************************************/
title, 'BOOSTER lattice 1.4 GeV, kBE3DVT4L1=0.001 rad';


! insert a kicker strength
kBE3DVT4L1=0.001; ! rad


! Match for very old working point, according to note PS/OP/Note 99-xx
!------------------------------------------------------------------------------------------
MATCH,sequence=psb3;
 vary, NAME=kKF, step = 0.0001;
 vary, NAME=kKD, step = 0.0001;
 constraint, range=#E, MUX=4.172, MUY=5.230;
 lmdif, calls = 10000, tolerance = 1.0E-21;
ENDMATCH;


! Print results on file: match_orbit.prt
! -----------------------------------------------------------------------------------------
assign, echo="../output/match_orbit_MUX4p172_MUY5p230_kBE3DVT4L1at0p001rad_lexpfromnote.prt";
print, text="match_orbit for MUX=4.172, MUY=5.230";
value,  kKF;
value,  kKD;
assign, echo=terminal;


! TWISS
! -----------------------------------------------------------------------------------------
 
PSHIFT=0;
 
/************************************************************
 * MAD uses pt as the 5th variable NOT delta-p.
 * The result is that all derivatives are with repect to pt.
 * This is the reason for the non-standard dispersion values
 * for non-relativistic machines, like the PSB
 ************************************************************/
beta=sqrt(1-1/beam->gamma^2);
disp:=beta*table(twiss,dx); ! Delta_x=Disp*Delta_p/p;
 
select, flag=twiss, clear;
select, flag=twiss, column=name, s,y,py; !alfx,alfy,betx,bety,mux,muy,disp;
 
twiss !, centre
      , DELTAP = PSHIFT
      , table=twiss
      , file='../output/twiss/psb_orbit_kBE3DVT4L1at0p001rad_lexpfromnote.twiss';
 

! PLOT
! -----------------------------------------------------------------------------------------

PLOT, table=twiss, VAXIS=Y,HAXIS=S, VMIN=-0.007,VMAX=0.007,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E, FILE='../output/ps/psb_orbit_kBE3DVT4L1at0p001rad_lexpfromnote';

YSEPT_START   := table(twiss,BE3.SMH15L1_START,y);
PYSEPT_START  := table(twiss,BE3.SMH15L1_START,py);
YSEPT_CENTER  := table(twiss,BE3.SMH15L1,y);
PYSEPT_CENTER := table(twiss,BE3.SMH15L1,py);


assign, echo="../output/geom/geom_rel_kBE3DVT4L1at0p001rad_lexpfromnote.txt";
print, text="! Optics function at the center of extraction septum BE3.SMH15L1";
value,  YSEPT_START;
value,  PYSEPT_START;
value,  YSEPT_CENTER;
value,  PYSEPT_CENTER;
assign, echo=terminal;

print, text="! Optics function at the center of extraction septum BE3.SMH15L1";
value,  YSEPT_START;
value,  PYSEPT_START;
value,  YSEPT_CENTER;
value,  PYSEPT_CENTER;


/******************************************************************************************
 * 
 *    S T O P !!!
 *
 ******************************************************************************************\

STOP;


